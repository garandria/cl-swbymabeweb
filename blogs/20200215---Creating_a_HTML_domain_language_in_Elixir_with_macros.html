<p>In this post we'll do a bit of exploration with <a class="link" href="https://elixir-lang.org" target="_blank">Elixir</a> macros and create our own little HTML DSL that will be part of a larger exploration project that develops a simple MVC based web framework.</p>
<p>This DSL should have a frontend and a backend that actually generates the HTML representation. For now it should use <a class="link" href="https://github.com/zambal/eml" target="_blank">Eml</a> to generate the HTML representation and the to_string conversion.<br /> However, it would be possible to also create an implementation that uses <a class="link" href="https://hexdocs.pm/eex/EEx.html" target="_blank">EEx</a> as a backend. And we could switch the backend without having the API user change its code.</p>
<p>So here is what we have to do to create a HTML DSL.</p>
<p>First we need a collection of tags. I have hardcoded them into a list:</p>
<div>
<pre class="elixir"><code>  @tags [:html, :head, :title, :base, :link, :meta, :style,
         :script, :noscript, :body, :div, :span, :article, ...]</code></pre>
</div>
<p>Then I want to allow to define tags in two styles. A one-liner style and a style with a multi-line body to be able to express multiple child elements.</p>
<div>
<pre class="elixir"><code># one-liner
span id: "1", class: "span-class", do: "my span text"

# multi-liner
div id: "1", class: "div-class" do
  span do: "my span text"
  span do: "my second text"
end</code></pre>
</div>
<p>We need two macros for this. The <code>do:</code> in the one-liner is seen just as an attribute to the macro. So we have to strip out the <code>do:</code> attribute and use it as body. The macro for this looks like this:</p>
<div>
<pre class="elixir"><code>  defmacro tag(name, attrs \\ []) do
    {inner, attrs} = Keyword.pop(attrs, :do)
     quote do: HouseStatUtil.HTML.tag(unquote(name),
                                      unquote(attrs), do: unquote(inner))
  end</code></pre>
</div>
<p>First we extract the value for the <code>:do</code> key in the <code>attrs</code> list and then pass the <code>name</code>, the remaining <code>attrs</code> and the extracted body as <code>inner</code> to the actual macro which looks like this and does the whole thing.</p>
<div>
<pre class="elixir"><code>  defmacro tag(name, attrs, do: inner) do
    parsed_inner = parse_inner_content(inner)
    
    quote do
      %E{tag: unquote(name),
         attrs: Enum.into(unquote(attrs), %{}),
         content: unquote(parsed_inner)}
    end
  end

  defp parse_inner_content({:__block__, _, items}), do: items
  defp parse_inner_content(inner), do: inner</code></pre>
</div>
<p>Here we get the first glimpse of Eml (the <code>%E{}</code> in there is an Eml structure type to create HTML tags). The helper function is to differentiate between having an AST as inner block or non-AST elements. But I don't want to go into more detail here.<br /> Instead I recommend reading the book <a class="link" href="https://pragprog.com/book/cmelixir/metaprogramming-elixir" target="_blank">Metaprogrammning Elixir</a> by Chris McCord which deals a lot with macros and explains how it works.</p>
<p>But something is still missing. We now have a <code>tag</code> macro. With this macro we can create HTML tags like this:</p>
<div>
<pre class="elixir"><code>tag "span", id: "1", class: "class", do: "foo"</code></pre>
</div>
<p>But that's not yet what we want. One step is missing. We have to create macros for each of the defined HTML tags. Remember the list of tags from above. Now we take this list and create macros from the atoms in the list like so:</p>
<div>
<pre class="elixir"><code>for tag <- @tags do
  defmacro unquote(tag)(attrs, do: inner) do
    tag = unquote(tag)
    quote do: HouseStatUtil.HTML.tag(unquote(tag), unquote(attrs), do: unquote(inner))
  end
 
  defmacro unquote(tag)(attrs \\ []) do
    tag = unquote(tag)
    quote do: HouseStatUtil.HTML.tag(unquote(tag), unquote(attrs))
  end
end
</code></pre>
</div>
<p>This creates three macros for each tag. I.e. for <code>span</code> it creates: <code>span/0</code>, <code>span/1</code> and <code>span/2</code>. The first two are because the <code>attrs</code> are optional but Elixir creates two function signatures for it. The third is a version that has a <code>do</code> block.</p>
<p>With all this put together we can create HTML as Elixir language syntax. Checkout the full <a class="link" href="https://github.com/mdbergmann/elixir_house_stat_util/blob/master/lib/house_stat_util/html.ex" target="_blank">module source</a> in the github repo.</p>
<h4 id="toc_16">Testing the DSL</h4>
<p>Of course we test this. This is a test case for a one-liner tag:</p>
<div>
<pre class="elixir"><code>  test "single element with attributes" do
    elem = input(id: "some-id", name: "some-name", value: "some-value")
    |&gt; render_to_string

    IO.inspect elem

    assert String.starts_with?(elem, "&lt;input")
    assert String.contains?(elem, ~s(id="some-id"))
    assert String.contains?(elem, ~s(name="some-name"))
    assert String.contains?(elem, ~s(value="some-value"))
    assert String.ends_with?(elem, "/&gt;")
  end</code></pre>
</div>
<p>This should be backend agnostic. So no matter which backend generated the HTML we want to see the test pass.</p>
<p>Here is a test case with inner tags:</p>
<div>
<pre class="elixir"><code>  test "multiple sub elements - container" do
    html_elem = html class: "foo" do
      head
      body class: "bar"
    end
    |&gt; render_to_string

    IO.inspect html_elem

    assert String.ends_with?(html_elem, 
      ~s())
  end</code></pre>
</div>
<p>The source file has more tests, but that should suffice as examples.</p>
<p>That was it. Thanks for reading.</p>
